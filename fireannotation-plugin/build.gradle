/*
 * Copyright (C) 2017 takahirom
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

group = 'com.github.takahirom.fireannotation.plugin'
version = '0.2.1'

def versionName = version

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "maven-publish"

apply plugin: 'com.jfrog.bintray'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    provided
    compile.extendsFrom provided
}

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    main {
        compileClasspath += configurations.provided
        java {
            srcDir 'build/generated-src/main/java'
        }
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile 'com.neenbedankt.gradle.plugins:android-apt:1.8'
    provided 'com.android.tools.build:gradle:2.2.3'
    compile 'org.javassist:javassist:3.20.0-GA'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    archives "org.apache.maven.wagon:wagon-ssh-external:2.2"
}

def siteUrl = 'https://github.com/takahirom/fire-annotation'
def gitUrl = 'https://github.com/takahirom/fire-annotation.git'

bintray {
    user = "takahirom"
    if (file("../key").exists()) {
        key = file("../key").text
    }
    configurations = ['archives'] //When uploading configuration files
    pkg {
        repo = 'maven'
        name = project.name
        desc = 'Simple tool which help you to implement Firebase Analytics'
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/takahirom/fire-annotation/issues'
        vcsUrl = gitUrl
        licenses = ['Apache-2.0']
        labels = ['aar', 'android', 'library']
        publicDownloadNumbers = true
        version {
            name = versionName //version
            desc = 'Simple tool which help you to implement Firebase Analytics' //Description
            released = new Date()
            vcsTag = versionName
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId group
            artifactId project.name
            version versionName
        }
    }
}
